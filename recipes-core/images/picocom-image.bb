SUMMARY = "standard image for picocom pc80x board"

IMAGE_INSTALL = "packagegroup-core-boot ${CORE_IMAGE_EXTRA_INSTALL} \
                e2fsprogs mtd-utils packagegroup-core-ssh-openssh \
                openssh-sftp-server e2fsprogs-resize2fs libpam libyang \
                libnetconf2 netopeer2 sysrepo libmxml libssh2 "

EXTRA_IMAGE_FEATURES += "package-management"

TOOLCHAIN_TARGET_TASK += "kernel-devsrc"

IMAGE_LINGUAS = " "

LICENSE = "MIT"

inherit core-image

IMAGE_ROOTFS_SIZE ?= "65536"
IMAGE_ROOTFS_EXTRA_SPACE:append = "${@bb.utils.contains("DISTRO_FEATURES", "systemd", " + 4096", "", d)}"

inherit extrausers
IMAGE_FEATURES:remove = "debug-tweaks"
EXTRA_IMAGE_FEATURES:remove = "allow-root-login allow-empty-password empty-root-password"

# password is generated by the command "openssl passwd -crypt <password>".
# root password is root, user password is user.
ROOT_PASSWORD = "4Xr5hrMvv82Ds"
USER_NAME = "user"
USER_PASSWORD = "Ioc0d4wx/lYy6"

# add sudo, rsync, xmllint command.
IMAGE_INSTALL:append = "sudo rsync libxml2-utils "

EXTRA_USERS_PARAMS = " \
    useradd -p '${USER_PASSWORD}' ${USER_NAME}; \
    usermod -p '${ROOT_PASSWORD}' root; \
    usermod -a -G sudo ${USER_NAME}; \
"
